//===== rAthena Script

prt_in,28,73,4	script	Wise Old Woman	1_F_ORIENT_04,{
	.NPCName$ = "^0055FF[Wise Old Woman]^000000\n";
	.zenyCost = 200000;
	.perCardCost = 25000;

	disable_items;

	mes .NPCName$;
	mes "Good day, young one.\n";
	mes "I have the power to remove cards that you have compounded onto your equipment.\n";
	mes "Does this idea please you?";
	next;

	.@option1$ = " ~ Yes, it does.";
	.@option2$ = " ~ What do you charge?";
	.@option3$ = "^777777 ~ No, thanks.^000000";

	switch (select(.@option1$ + ":" + .@option2$ + ":" + .@option3$)) {
		case 1:
			setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;

			for (.@i = 1; .@i <= 10; .@i = .@i + 1) {
				if (getequipisequiped(.@indices[.@i]))
					.@menu$ = .@menu$ + " ~ " + F_getpositionname(.@indices[.@i]) + " (^0055FF" + getequipname(.@indices[.@i]) + "^000000)";

				.@menu$ = .@menu$ + ":";
			}

			if (.@menu$ == "") {
				mes .NPCName$;
				mes "Young one...\n";
				mes "You aren't wearing anything...\n";
				mes "Aren't you ashamed to walk around naked?\n";
				mes "Ah, the good old days...";
				close;
			}

			mes .NPCName$;
			mes "Very well.\n";
			mes "Which item shall I examine for you?";
			next;

			.@part = .@indices[select(.@menu$)];

			if (!getequipisequiped(.@part)) {
				mes .NPCName$;
				mes "Young one...\n";
				mes "You aren't wearing anything there I can remove cards from.";
				close;
			}

			if (getequipcardcnt(.@part) == 0) {
				mes .NPCName$;
				mes "Young one...\n";
				mes "There are no cards compounded on this item.\n";
				mes "I can do nothing with it, I'm afraid.";
				close;
			}

			.@cardCount = getequipcardcnt(.@part);
			
			if (!checkweight(1202, .@cardCount + 1)) {
				mes .NPCName$;
				mes "Wait just a minute!\n";
				mes "You're carrying too much stuff.\n";
				mes "Come again when you've sorted that out.";
				close;
			}

			if (.@cardCount == 1)
				.@plural$ = "";
			else
				.@plural$ = "s";

			mes .NPCName$;
			mes "Your ^0055FF" + getequipname(.@part) + "^000000 has ^0055FF" + .@cardCount + "^000000 card" + .@plural$ + " compounded on it.\n";
			mes "To perform my magic, I will need ^0055FF" + callfunc("F_InsertComma", .@cardCount * .perCardCost + .zenyCost) + "^000000 Zeny, a ^0055FFStar Crumb^000000 and a ^0055FFYellow Gemstone^000000.";
			next;

			.@option1$ = " ~ Yes please, do it!";
			.@option2$ = "^777777 ~ Never mind.^000000";

			if (select(.@option1$ + ":" + .@option2$) == 2) {
				mes .NPCName$;
				mes "Very well.\n";
				mes "Return at once if you seek my services.";
				close;
			}

			if (zeny < .@cardCount * .perCardCost + .zenyCost) {
				mes .NPCName$;
				mes "Young one...\n";
				mes "You don't have the Zeny I require to work my magic.\n";
				mes "Come again when you do.";
				close;
			}

			if (countitem(1000) < 1 || countitem(715) < 1) {
				mes .NPCName$;
				mes "Young one...\n";
				mes "You do not have all the items I require to work my magic.\n";
				mes "Come again when you do.";
				close;
			}

			set zeny, zeny - .@cardCount * .perCardCost + .zenyCost;
			delitem 1000, 1; // Star_Crumb
			delitem 715, 1; // Yellow_Gemstone
			successremovecards .@part;
			
			mes .NPCName$;
			mes "The process was a success.\n";
			mes "Here are your cards and your item.\n";
			mes "Farewell.";
			close;
		case 2:
			mes .NPCName$;
			mes "I charge a flat fee of ^0055FF" + callfunc("F_InsertComma", .zenyCost) + "^000000 Zeny, plus ^0055FF" + callfunc("F_InsertComma", .perCardCost) + "^000000 Zeny for each card I remove from the item.\n";
			mes "In addition, I need a ^0055FFStar Crumb^000000 and a ^0055FFYellow Gemstone^000000 to work my magic.";
			close;
		case 3:
			mes .NPCName$;
			mes "Very well.\n";
			mes "Return at once if you seek my services.";
			close;
	}
}
