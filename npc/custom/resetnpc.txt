//===== rAthena Script

prontera,142,179,7	script	Reset Girl	4_F_TELEPORTER,{
	setarray .@services[1], 50000, 50000, 75000;

	.@option1$ = " ~ Reset Stats (^0055FF" + callfunc("F_InsertComma", .@services[1]) + "^000000 Zeny)";
	.@option2$ = " ~ Reset Skills (^0055FF" + callfunc("F_InsertComma", .@services[2]) + "^000000 Zeny)";
	.@option3$ = " ~ Reset Stats & Skills (^0055FF" + callfunc("F_InsertComma", .@services[3]) + "^000000 Zeny)";
	.@option4$ = "^777777 ~ Cancel^000000";
	.@service = select(.@option1$ + ":" + .@option2$ + ":" + .@option3$ + ":" + .@option4$);

	if (.@service > 3)
		end;
	
	.@price = .@services[.@service];

	if (zeny < .@price) {
		message strcharinfo(0), "You need " + callfunc("F_InsertComma", .@price) + " Zeny for that.";
		end;
	}

	switch(.@service) {
		case 1:
			resetstatus;
			message strcharinfo(0), "Your Stats were reset.";
			specialeffect2 EF_BLOODDRAIN;
			break;
		case 2:
			resetskill;
			callfunc "F_GetPlatinumSkills";
			message strcharinfo(0), "Your Skills were reset.";
			specialeffect2 EF_ENERGYDRAIN;
			break;
		default:
			resetstatus;
			resetskill;
			callfunc "F_GetPlatinumSkills";
			message strcharinfo(0), "Your Stats & Skills were reset.";
			specialeffect2 EF_BLOODDRAIN;
			sleep2 50;
			specialeffect2 EF_ENERGYDRAIN;
	}

	zeny = zeny - .@price;
	end;
}
